apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'testdroid'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "bbct.android.common"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
        }
    }
    productFlavors {
        lite {
            applicationId "bbct.android"
            versionCode 15
            versionName "0.6.2"
        }
        premium {
            applicationId "bbct.android.premium"
            versionCode 14
            versionName "0.6.2"
        }
    }
    def Properties props = new Properties()
    def propFile = new File('bbct.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD')
                && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'bbct.properties found but SIGNING entries are missing'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'bbct.properties not found'
        android.buildTypes.release.signingConfig = null
    }
}

testdroid {
    deviceGroup 'Android'
    mode 'FULL_RUN'
    projectName 'BBCT Lite'

    fullRunConfig {
        instrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'
        limitationType = 'CLASS'
        limitationValue = 'bbct.android.common.functional.test.LaunchTest'
    }

    def Properties props = new Properties()
    def propFile = new File('bbct.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props!=null && props.containsKey('TESTDROID_USERNAME') && props.containsKey('TESTDROID_PASSWORD')) {
            testdroid.username = props['TESTDROID_USERNAME']
            testdroid.password = props['TESTDROID_PASSWORD']
        } else {
            println 'bbct.properties found but TESTDROID entries are missing'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'bbct.properties not found'
        android.buildTypes.release.signingConfig = null
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.google.android.gms:play-services:6.1.71'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.jakewharton:butterknife:5.1.2'
    liteCompile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
    androidTestCompile ('com.squareup:fest-android:1.0.8') {
        exclude group: 'com.android.support'
    }
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
}
