apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'testdroid'
apply plugin: 'com.google.firebase.crashlytics'

buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath 'com.testdroid:gradle:1.3.0'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

android {
    compileSdkVersion 31
    flavorDimensions "tier"

    defaultConfig {
        applicationId "bbct.android.common"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 24
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
            debuggable true
        }
    }
    productFlavors {
        lite {
            dimension "tier"
            applicationId "bbct.android"
        }
        premium {
            dimension "tier"
            applicationId "bbct.android.premium"
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

testdroid {
    mode 'FULL_RUN'
    projectName 'BBCT Lite'

    fullRunConfig {
        instrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'
    }
}

Properties props = new Properties()
def propFile = new File('bbct.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null) {
        if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD')
                && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'bbct.properties found but SIGNING entries are missing'
            android.buildTypes.release.signingConfig = null
        }

        if (props.containsKey('TESTDROID_USERNAME') && props.containsKey('TESTDROID_PASSWORD')) {
            testdroid.username = props['TESTDROID_USERNAME']
            testdroid.password = props['TESTDROID_PASSWORD']
        } else {
            println 'bbct.properties found but TESTDROID entries are missing'
        }

        if (props.containsKey('TESTDROID_PACKAGE')) {
            testdroid.fullRunConfig.limitationType = 'PACKAGE'
            testdroid.fullRunConfig.limitationValue = props['TESTDROID_PACKAGE']
            println 'Setting test package: ' + props['TESTDROID_PACKAGE']
        }

        if (props.containsKey('TESTDROID_CLASS')) {
            testdroid.fullRunConfig.limitationType = 'CLASS'
            testdroid.fullRunConfig.limitationValue = props['TESTDROID_CLASS']
            println 'Setting test class: ' + props['TESTDROID_CLASS']
        }

        if (props.containsKey('TESTDROID_DEVICE_GROUP')) {
            testdroid.deviceGroup = props['TESTDROID_DEVICE_GROUP']
        } else {
            println '\'deviceGroup\' not specified'
        }
    }
} else {
    println 'bbct.properties not found'
}

ext {
    annotationVersion = '1.0.0'
    appcompatVersion = '1.3.1'
    archCompVersion = '1.1.1'
    butterknifeVersion = '10.0.0'
    espressoVersion = '3.4.0'
    fastlaneVersion = '1.0.0'
    firebaseAnalyticsVersion = '19.0.1'
    firebaseCrashlyticsVersion = '18.2.1'
    fragmentVersion = '1.3.6'
    googleServiceAdsVersion = '20.4.0'
    junitVersion = '4.13.1'
    lifecycleVersion = '2.4.0-beta01'
    materialVersion = '1.4.0'
    roomVersion = '2.4.0-alpha04'
    testLibVersion = '1.4.0'
    uiautomatorVersion = '2.2.0'
}

dependencies {
    implementation project(':lib')
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "com.google.android.material:material:${materialVersion}"
    implementation "androidx.room:room-runtime:${roomVersion}"
    implementation "androidx.lifecycle:lifecycle-livedata:${lifecycleVersion}"
    implementation "com.google.firebase:firebase-analytics:${firebaseAnalyticsVersion}"
    implementation "com.google.firebase:firebase-crashlytics:${firebaseCrashlyticsVersion}"
    implementation "com.jakewharton:butterknife:${butterknifeVersion}"

    liteImplementation "com.google.android.gms:play-services-ads:${googleServiceAdsVersion}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"
    annotationProcessor "androidx.room:room-compiler:${roomVersion}"

    testImplementation "junit:junit:${junitVersion}"
    testImplementation "androidx.room:room-testing:${roomVersion}"

    androidTestImplementation "androidx.annotation:annotation:${annotationVersion}"
    androidTestImplementation "androidx.test:runner:${testLibVersion}"
    androidTestImplementation "androidx.test:rules:${testLibVersion}"
    androidTestImplementation "androidx.fragment:fragment-testing:${fragmentVersion}"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:${uiautomatorVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"
    androidTestImplementation "tools.fastlane:screengrab:${fastlaneVersion}"
}
