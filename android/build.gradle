apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

android {
    compileSdkVersion 32
    flavorDimensions "tier"

    defaultConfig {
        applicationId "bbct.android.common"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 24
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        multiDexEnabled true
        buildConfigField "String", "API_BASE_URL", "\"http://10.0.2.2:8000/api/\""
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
            debuggable true
        }
    }
    productFlavors {
        lite {
            dimension "tier"
            applicationId "bbct.android"
        }
        premium {
            dimension "tier"
            applicationId "bbct.android.premium"
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

Properties props = new Properties()
def propFile = new File('bbct.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null) {
        if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD')
                && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'bbct.properties found but SIGNING entries are missing'
            android.buildTypes.release.signingConfig = null
        }
    }
} else {
    println 'bbct.properties not found'
}

ext {
    annotationVersion = '1.0.0'
    appcompatVersion = '1.4.1'
    archCompVersion = '1.1.1'
    butterknifeVersion = '10.0.0'
    espressoVersion = '3.4.0'
    fastlaneVersion = '2.1.1'
    firebaseAnalyticsVersion = '20.1.1'
    firebaseCrashlyticsVersion = '18.2.9'
    fragmentVersion = '1.4.1'
    googleServiceAdsVersion = '20.6.0'
    junitVersion = '4.13.1'
    lifecycleVersion = '2.5.0-alpha04'
    materialVersion = '1.5.0'
    retrofitVersion = '2.9.0'
    roomVersion = '2.5.0-alpha01'
    testLibVersion = '1.4.0'
    uiautomatorVersion = '2.2.0'
}

dependencies {
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "androidx.lifecycle:lifecycle-livedata:${lifecycleVersion}"
    implementation "androidx.room:room-runtime:${roomVersion}"
    implementation "com.google.android.material:material:${materialVersion}"
    implementation "com.google.firebase:firebase-analytics:${firebaseAnalyticsVersion}"
    implementation "com.google.firebase:firebase-crashlytics:${firebaseCrashlyticsVersion}"
    implementation "com.jakewharton:butterknife:${butterknifeVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    liteImplementation "com.google.android.gms:play-services-ads:${googleServiceAdsVersion}"

    annotationProcessor "androidx.room:room-compiler:${roomVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"

    testImplementation "androidx.room:room-testing:${roomVersion}"
    testImplementation "junit:junit:${junitVersion}"

    androidTestImplementation "androidx.annotation:annotation:${annotationVersion}"
    androidTestImplementation "androidx.fragment:fragment-testing:${fragmentVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:${uiautomatorVersion}"
    androidTestImplementation "androidx.test:rules:${testLibVersion}"
    androidTestImplementation "androidx.test:runner:${testLibVersion}"
    androidTestImplementation "tools.fastlane:screengrab:2.1.1"
}
